plugins {
    id 'net.fabricmc.fabric-loom' version '1.14-SNAPSHOT'
    id 'maven-publish'
    id 'org.jetbrains.kotlin.jvm'
}

def targetVersion = project.hasProperty("mc_version") ? project.getProperty("mc_version") : "v26_1"

// ========================
// 通用 MC 版本解析（支持 1.x / 26.x / 未来）
// ========================
def parseMcVersion = { String target ->
    def parts = target.replace('v', '').split('_')*.toInteger()
    return [
            major     : parts[0],
            minor     : parts.size() > 1 ? parts[1] : 0,
            patchStart: parts.size() > 2 ? parts[2] : 0,
            patchEnd  : parts.size() > 3 ? parts[3] : null
    ]
}

// ========================
// 拼接 mod 版本号
// ========================
def modVersionBase = project.hasProperty("mod_version") ? project.mod_version : "1.0.0"

def finalModVersion = {
    def v = parseMcVersion(targetVersion)

    def start = "${v.major}.${v.minor}" + (v.patchStart > 0 ? ".${v.patchStart}" : "")
    if (v.patchEnd != null) {
        def end = "${v.major}.${v.minor}" + (v.patchEnd > 0 ? ".${v.patchEnd}" : "")
        return "${modVersionBase}+${start}-${end}"
    }
    return "${modVersionBase}+${start}"
}()

version = finalModVersion
group = project.maven_group

base {
    archivesName = project.archives_base_name
}

// ========================
// 版本映射表
// ========================
def fabricApiVersions = [
        "v26_1": "0.141.2+26.1"
]

def modMenuVersions = [
        "v26_1": "v18.0.0-alpha.2"
]

def clothConfigVersions = [
        "v26_1": "21.11.151"
]

def loaderVersion = [
        "v26_1": "0.18.4"
]

// ========================
// fabric.mod.json 用版本范围
// ========================
def resolvedMinecraftVersion = {
    def v = parseMcVersion(targetVersion)

    def start = "${v.major}.${v.minor}" + (v.patchStart > 0 ? ".${v.patchStart}" : "")
    if (v.patchEnd != null) {
        def end = "${v.major}.${v.minor}" + (v.patchEnd > 0 ? ".${v.patchEnd}" : "")
        return ">=${start} <=${end}"
    }
    return start
}()

// ========================
// Maven 依赖用 MC 版本
// ========================
def dependencyMinecraftVersion = {
    def v = parseMcVersion(targetVersion)
    return "${v.major}.${v.minor}" + (v.patchStart > 0 ? ".${v.patchStart}" : "")
}()

// ========================
// 源码目录版本
// ========================
def minVersionForFiles = {
    def v = parseMcVersion(targetVersion)
    return v.patchEnd != null
            ? "v${v.major}_${v.minor}_${v.patchStart}"
            : targetVersion
}()

// ========================
// 校验 targetVersion 是否存在
// ========================
def requiredKeys = [fabricApiVersions, modMenuVersions, clothConfigVersions, loaderVersion]
requiredKeys.each {
    if (!it.containsKey(targetVersion)) {
        throw new GradleException("missing targetVersion = ${targetVersion}")
    }
}

// ========================
// SourceSets
// ========================
sourceSets {
    main {
        java {
            setSrcDirs(["src/main/java"])
            def versionedPath = "src/main/${targetVersion}/java"
            if (file(versionedPath).exists()) {
                srcDir versionedPath
            }
        }
    }
}

loom {
    mods {
        "vein_mine" {
            sourceSet sourceSets.main
        }
    }
}

// ========================
// Repositories
// ========================
repositories {
    maven { url 'https://maven.terraformersmc.com/' }
    maven { url 'https://maven.shedaniel.me/' }
    mavenCentral()
}

// ========================
// Dependencies
// ========================
dependencies {
    //minecraft "com.mojang:minecraft:${dependencyMinecraftVersion}"
    minecraft "com.mojang:minecraft:26.1-snapshot-2"
    implementation "net.fabricmc:fabric-loader:${loaderVersion[targetVersion]}"
//    api("me.shedaniel.cloth:cloth-config-fabric:${clothConfigVersions[targetVersion]}") {
//        exclude(group: "net.fabricmc.fabric-api")
//    }
//    compileOnly "com.terraformersmc:modmenu:${modMenuVersions[targetVersion]}"
    implementation "net.fabricmc.fabric-api:fabric-api:${fabricApiVersions[targetVersion]}"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    //mappings loom.officialMojangMappings()
}

// ========================
// Resources
// ========================
processResources {
    inputs.property "version", project.version
    inputs.property "loader_version", loaderVersion[targetVersion]
    filteringCharset "UTF-8"

    filesMatching("fabric.mod.json") {
        expand(
                version: project.version,
                minecraft_version: resolvedMinecraftVersion,
                loader_version: loaderVersion[targetVersion],
                modmenu_version: modMenuVersions[targetVersion],
                cloth_config_version: clothConfigVersions[targetVersion],
                api_version: fabricApiVersions[targetVersion],
                fabric: fabricApiVersions[targetVersion],
                targetVersion: targetVersion,
                versionForFiles: minVersionForFiles
        )
    }
}

// ========================
// Java / Kotlin
// ========================
def targetJavaVersion = project.hasProperty("targetJavaVersion")
        ? Integer.parseInt(project.targetJavaVersion)
        : 25

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    it.options.release.set(targetJavaVersion)
}

java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
    withSourcesJar()
}

kotlin {
    jvmToolchain(project.hasProperty("kotlin_jvm_toolchain")
            ? Integer.parseInt(project.kotlin_jvm_toolchain)
            : 25)
}
